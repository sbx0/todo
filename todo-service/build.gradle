plugins {
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot
    id 'org.springframework.boot' version '3.0.6'
    // https://mvnrepository.com/artifact/io.spring.dependency-management/io.spring.dependency-management.gradle.plugin
    id 'io.spring.dependency-management' version '1.1.0'
    // https://mvnrepository.com/artifact/org.asciidoctor.jvm.convert/org.asciidoctor.jvm.convert.gradle.plugin
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
    id 'java'
}

group = 'cn.sbx0.todo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.17'

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-data'
    implementation 'org.springframework.data:spring-data-redis'

    implementation 'org.flywaydb:flyway-mysql'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

    // https://mvnrepository.com/artifact/io.lettuce/lettuce-core
    implementation group: 'io.lettuce', name: 'lettuce-core', version: '6.2.3.RELEASE'
    // https://mvnrepository.com/artifact/com.theokanning.openai-gpt3-java/service
    implementation 'com.theokanning.openai-gpt3-java:service:0.12.0'

    runtimeOnly 'com.mysql:mysql-connector-j'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // https://mvnrepository.com/artifact/org.mockito/mockito-inline
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompileOnly 'org.projectlombok:lombok'
    testRuntimeOnly 'com.h2database:h2'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
    outputs.dir snippetsDir
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}
